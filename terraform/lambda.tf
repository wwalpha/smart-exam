# ----------------------------------------------------------------------------------------------
# CloudWatch Log Group for Lambda.
# ----------------------------------------------------------------------------------------------
resource "aws_cloudwatch_log_group" "lambda" {
  name              = "/aws/lambda/${local.lambda_function_name}"
  retention_in_days = 30
}

# ----------------------------------------------------------------------------------------------
# Dummy Lambda artifact generated by Terraform.
# ----------------------------------------------------------------------------------------------
data "archive_file" "lambda_dummy" {
  type        = "zip"
  source_dir  = "${path.module}/dummy"
  output_path = "${path.module}/.terraform/dummy.zip"
}

# ----------------------------------------------------------------------------------------------
# Lambda function (uses Terraform-generated dummy artifact).
# ----------------------------------------------------------------------------------------------
resource "aws_lambda_function" "api" {
  function_name = local.lambda_function_name
  role          = aws_iam_role.lambda.arn
  runtime       = "nodejs22.x"
  handler       = "index.handler"

  filename         = data.archive_file.lambda_dummy.output_path
  source_code_hash = data.archive_file.lambda_dummy.output_base64sha256

  lifecycle {
    ignore_changes = [source_code_hash]
  }

  timeout     = 300
  memory_size = 1024

  environment {
    variables = {
      FILES_BUCKET_NAME = aws_s3_bucket.files.bucket

      TABLE_MATERIALS          = aws_dynamodb_table.materials.name
      TABLE_MATERIAL_QUESTIONS = aws_dynamodb_table.material_questions.name
      TABLE_KANJI              = aws_dynamodb_table.kanji.name
      TABLE_EXAM_CANDIDATES    = aws_dynamodb_table.exam_candidates.name
      TABLE_EXAM_HISTORIES     = aws_dynamodb_table.exam_histories.name
      TABLE_EXAMS              = aws_dynamodb_table.exam.name
      TABLE_EXAM_DETAILS       = aws_dynamodb_table.exam_details.name
      BEDROCK_REGION           = "us-east-1"
    }
  }

  depends_on = [aws_cloudwatch_log_group.lambda]
}
